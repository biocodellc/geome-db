buildscript {
    repositories {
        maven {
            url "http://repo.biocodellc.com/repository/maven-private/"
            credentials {
                username mavenUser
                password mavenPass
            }
        }
        mavenLocal()
    }
    dependencies {
        classpath 'com.github.fgiannesini.libsass.gradle.plugin:libsass-gradle-plugin:+'
        classpath "org.biocode:gradle-fims:1.1.8"
    }
}

apply plugin: "org.biocode.fims-web"

import org.biocode.gradle.web.tasks.RestartRemoteJettyTask
import org.biocode.gradle.web.tasks.HotDeployRemoteJettyTask

ext.fimsCommonsVersion = "0.2.10"
ext.fimsSequencesVersion = "0.1.4"

fims {
    maven {
        username mavenUser
        password mavenPass
    }
}

repositories {
    add(fims.mavenFims())
    mavenLocal()
}

// need to evaluate childProjects first so we can add the childProject server dependencies to the server configuration
evaluationDependsOnChildren()

dependencies {
    server('org.glassfish.jersey.ext:jersey-spring3:2.25') {
        exclude module: 'bean-validator'
        exclude module: 'asm-all-repackaged'
    }
    server 'org.glassfish.jersey.media:jersey-media-multipart:2.25'

    server 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate5:2.8.4'
    server 'org.springframework.data:spring-data-jpa:1.10.1.RELEASE'
    server group: 'org.hibernate.javax.persistence', name: 'hibernate-jpa-2.1-api', version: '1.0.0.Final'

    server 'javax.servlet:servlet-api:2.4'
    server 'org.slf4j:slf4j-log4j12:1.7.19'
    server 'com.googlecode.json-simple:json-simple:1.1.1'

    server('org.apache.commons:commons-digester3:3.2') {
        exclude module: 'asm'
    }
    server 'commons-cli:commons-cli:1.2'
    server group: 'org.apache.commons', name: 'commons-exec', version: '1.3'

    server group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'

    provided 'org.eclipse.jetty:jetty-servlet:9.3.3.v20150827'

    compile composite.resolveDependency("org.biocode:biocode-fims-commons:${fimsCommonsVersion}", "biocode-fims-commons")
    compile composite.resolveDependency("org.biocode:biocode-fims-sequences:${fimsSequencesVersion}", "biocode-fims-sequences")

    testCompile "junit:junit-dep:4.10"

    subprojects.each {
        project.configurations.server.dependencies.addAll(it.configurations.server.dependencies)
        project.configurations.serverRuntime.dependencies.addAll(it.configurations.serverRuntime.dependencies)
    }
}

//=============================
//= ssh configuration
//=============================

// jetty ssh plugin
remotes {
    biscicol {
        host = 'biscicol.org'
        user = biscicolUser
        password = biscicolPassword
    }
}

//=============================
//= deployment configuration
//=============================

updateDependencies {
    remote = remotes.biscicol
    location = remoteLibsDirectory
}

updateDependenciesDev {
    remote = remotes.biscicol
    location = remoteDevLibsDirectory
}

deployFims {
    remote = remotes.biscicol
    remoteLibsDir = remoteLibsDirectory
    remoteWarDir = remoteWarDirectory
}

deployFimsDev {
    remote = remotes.biscicol
    remoteLibsDir = remoteDevLibsDirectory
    remoteWarDir = remoteWarDevDirectory
}

deployFimsLocal {
    deployDir = localWarDirectory
}

task hotDeployFimsDev(type: HotDeployRemoteJettyTask, dependsOn: "deployFimsDev") {
    group = 'Fims'
    remoteWarDir = remoteWarDevDirectory
    remote = remotes.biscicol
}

task hotDeployFims(type: HotDeployRemoteJettyTask, dependsOn: "deployFims") {
    group = 'Fims'
    remoteWarDir = remoteWarDirectory
    remote = remotes.biscicol
}

task restartFims(type: RestartRemoteJettyTask) {
    group = 'Fims'
    jettyPath = biscicolJettyPath
    remote = remotes.biscicol
}

task restartFimsDev(type: RestartRemoteJettyTask) {
    group = 'Fims'
    jettyPath = biscicolJettyDevPath
    remote = remotes.biscicol
}

//=============================
//= war configuration
//=============================

war {
    from ("${projectDir}/docs/helpDocumentation.pdf", {
        into 'docs'
    })
}

fatWar {
    from ("${projectDir}/docs/helpDocumentation.pdf", {
        into 'docs'
    })
}

//===========================
//= js configuration
//===========================

// specify files that require a specific loading order
ext.externLibsOrdered = ["${webAppDir}/js/externalLibs/lodash.js", "${webAppDir}/js/externalLibs/papaparse.min.js",
                         "${webAppDir}/js/externalLibs/xlsx.min.js", "${webAppDir}/js/externalLibs/xlsx-reader.js",
                         "${webAppDir}/js/externalLibs/leaflet.js"]
javascript.source {
    externLibs {
        js {
            srcDir "src/main/web/js/externalLibs"
            include "*.js"
            // exclude any file that is in the externLibsOrdered list
            exclude {
                it.file in files(externLibsOrdered).files
            }
        }
    }
    app {
        js {
            srcDir "src/main/web/app"
            include "**/*.js"
            exclude "app.js"
            exclude "app.module.js"
            exclude "app.constants.js.template"
            exclude "**/module.js"
            exclude "**/*.module.js"
        }
    }
    js {
        js {
            srcDir "src/main/web/js"
            include "*.js"
            exclude "fims-analytics.js"
            exclude "autotrack.js"
        }
    }
    modules {
        js {
            srcDir "src/main/web/app"
            include "**/module.js"
            include "**/*.module.js"
        }
    }
}

combineJs {
    encoding = "UTF-8"
    source = files("${webAppDirName}/app/app.module.js", javascript.source.modules.js.files, javascript.source.app.js.files, javascript.source.js.js.files)
    dest = file("${webAppDirName}/js/all.js")
}

jsExternalLibs {
    source = externLibsOrdered + javascript.source.externLibs.js.files
    closure {
        compilerOptions.language = "ECMASCRIPT5"
        warningLevel = "QUIET"
    }
}
jsApp {
    source = files("src/main/web/app/app.module.js", javascript.source.modules.js.files, javascript.source.app.js.files, javascript.source.js.js.files)
    closure {
        compilerOptions.language = "ECMASCRIPT5"
    }
}

//=================================
//= angular constants configuration
//=================================

populateJsProps {
    propsFile file("src/main/resources/geome-db.props")
    templateFile file("${webAppDir}/app/app.constants.js.template")
    constantsFile file("${webAppDir}/app/app.constants.js")
    propsMap = ['mapboxToken': 'mapboxAccessToken',
                'projectId'  : 'projectId']
}
